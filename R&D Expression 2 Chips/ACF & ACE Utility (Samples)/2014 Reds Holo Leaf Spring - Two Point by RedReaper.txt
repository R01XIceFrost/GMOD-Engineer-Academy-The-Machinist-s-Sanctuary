@name Reds Holo Leaf Spring - Two Point
@inputs [A B]:entity
@persist I Scale:vector Low High Scalefix LowScale HighScale
@trigger none
#@model models/sprops/misc/fittings/cred_12_9_short.mdl

#[

INFO:

Two point leaf spring. 
This one pivots between two end points. 
This is designed for leaf spring bogies, like the rear axles of 6x4 trucks, where one leaf spring handles two axles, or for tanks. 
You can also use it on old car front axles. It can be used, if you desire, for a single axle, 
by making there two "shackle points" on your vehicle and attaching the chip to the axle.

]#

interval(25)

if(dupefinished()) {reset()}
elseif(first())
    {
        Size = 1
        Width = 2
        Model = "models/sprops/trans/misc/leaf_48.mdl"
        #Ignore everything below here
        Scale = vec(Size,Width,Size)
        holoCreate(1,entity():pos())
        holoModel(1,Model)
        holoParent(1,entity())
        holoScale(1,Scale)
        holoAng(1,entity():toWorld(ang(180,0,0)))
        Scalefix = 0.25
        Low = Scale:z() * 0.5
        High = Scale:z() * 2.5
        LowScale = -(High*2)
        HighScale = -(High*4)
    }
EndpointA = A:massCenter()
EndpointB = B:massCenter()

Endpoint = (EndpointA+EndpointB)/2
Axis = entity():toLocalAxis(EndpointA-EndpointB)
Atan = atan(-Axis:z(),Axis:x())
holoAng(1,entity():toWorld(ang(180+Atan,0,0)))
holoPos(1,(entity():pos() + entity():toWorld(vec(0,0,clamp(entity():toLocal(Endpoint):z()*1.5,LowScale,HighScale))))/2)
if(I>=3)
    {
           
        
        Dif = entity():toLocal((entity():pos() + entity():toWorld(vec(0,0,entity():toLocal(Endpoint):z())))/2):length()
        holoScale(1,Scale:setZ(clamp(Dif * Scalefix,Low,High)))
        I = 0
    }
else {I++}
