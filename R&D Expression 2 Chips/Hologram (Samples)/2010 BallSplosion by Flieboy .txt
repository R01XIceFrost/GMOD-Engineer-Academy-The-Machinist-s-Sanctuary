@name BallSplosion
@persist Click Worlds:array
@trigger 

#[

INFO

 When you right click a bunch of balls explode from the position, they have physics and roll around.

]#

runOnTick(1)

Click = owner():keyAttack2()
HOLOS = 25
SCALE = 0.5
SPACE = 2

if (first()) {
    for (I = 1, HOLOS) {
        holoCreate(I)
        holoModel(I, "hqicosphere2")
        holoScale(I, vec(1,1,1) * SCALE)
    }
}

if (Click & $Click) {
    for (I = 1, HOLOS) {

        holoPos(I, owner():aimPos())

        Rand = randvec(-1, 1)
        Worlds[I,vector] = (owner():aimNormal() + Rand) * SPACE
    }
}

for (I = 1, HOLOS) {

    holoPos(I, holoEntity(I):toWorld(Worlds[I,vector]))

    Worlds[I,vector] = Worlds[I,vector] - vec(0,0,0.05)
    Worlds[I,vector] = Worlds[I,vector] * 0.99

    RD = rangerOffset(6 * SCALE, holoEntity(I):pos(), vec(0,0,-1))
    RV = rangerOffset(6 * SCALE, holoEntity(I):pos(), Worlds[I,vector]:setZ(0))

    if (RD:hit()) {
        Dir = RD:position() + vec(0,0,6 * SCALE) - holoEntity(I):pos()
        Worlds[I,vector] = Worlds[I,vector]:setZ(Dir:z())
    }

    if (RV:hit()) {
        Worlds[I,vector] = Worlds[I,vector] + RV:hitNormal()
    }
}
