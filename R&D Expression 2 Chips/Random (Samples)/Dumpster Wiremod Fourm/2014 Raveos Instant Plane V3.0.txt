@name Raveos Instant Plane V3.0
@inputs 
@outputs S
@persist E:entity T P Y S Prop:entity H Mul
#Written by Raveo.

#[

INFO:

Unoptimised fully but V4. still has a few issues that need sorting out. 
Copy the E2 code, save it into the expression 2 tool, place it on any chair. Mouse aim to fly, W&S control speed.

]#

if(first()|dupefinished())
{
    runOnTick(1)
    E=entity():isWeldedTo()
    E:setMass(50000)
    function holoC(N:number,Pos:vector,Scale:vector,Ang:angle,Colour:vector,Model:string,Parent:entity)
    {
        holoCreate(N,Pos,Scale,Ang,Colour)
        holoModel(N,Model)
        holoParent(N,Parent)
    }
    Colour=randvec(vec(),vec(255))
    holoC(1,E:toWorld(vec(0,0,0)),vec(2,7,2),E:toWorld(ang(0,0,0)),Colour,"cube",E)
    holoC(2,E:toWorld(vec(0,48,0)),vec(2,2,1),E:toWorld(ang(0,0,-90)),Colour,"hq_cubinder",E)
    holoC(3,E:toWorld(vec(0,66,0)),vec(2,2,2),E:toWorld(ang(0,0,-90)),Colour,"hq_cone",E)
    holoC(4,E:toWorld(vec(0,66,0)),vec(0.5,0.01,5),E:toWorld(ang(randint(1,360),0,0)),vec(0,0,0),"cube",E)
    holoC(5,E:toWorld(vec(0,-90,0)),vec(2,2,8),E:toWorld(ang(0,0,90)),Colour,"pyramid",E)
    holoC(6,E:toWorld(vec(0,0,0)),vec(0.2,20,3),E:toWorld(ang(90,0,90)),Colour,"prism",E)
    holoC(7,E:toWorld(vec(0,-120,0)),vec(0.2,10,3),E:toWorld(ang(90,0,-90)),Colour,"tetra",E)
    holoC(8,E:toWorld(vec(0,-129,10)),vec(0.1,1.5,1.5),E:toWorld(ang(0,0,90)),Colour,"prism",E)
    holoC(9,E:toWorld(vec(-12,25,-20)),vec(0.2,0.2,1.8),E:toWorld(ang(45,0,0)),Colour,"hq_cylinder",E)
    holoC(10,E:toWorld(vec(12,25,-20)),vec(0.2,0.2,1.8),E:toWorld(ang(-45,0,0)),Colour,"hq_cylinder",E) 
    holoC(11,E:toWorld(vec(18,25,-27)),vec(1,1,0.2),E:toWorld(ang(90,0,0)),vec(0,0,0),"hq_cylinder",E)
    holoC(12,E:toWorld(vec(-18,25,-27)),vec(1,1,0.2),E:toWorld(ang(90,0,0)),vec(0,0,0),"hq_cylinder",E)
    holoC(13,E:toWorld(vec(-0,-45,-17)),vec(0.5,0.2,2),E:toWorld(ang(0,0,-30)),Colour,"hq_cylinder",E)
    holoC(14,E:toWorld(vec(-0,-50,-27)),vec(1,1,0.2),E:toWorld(ang(90,0,0)),vec(0,0,0),"hq_cylinder",E)
    R=players()
    R:insertEntity(1,E)
    rangerPersist(1)
    rangerFilter(R)
}
R1=rangerOffset(999999,E:pos(),vec(0,0,-1))
H=R1:distance()
V=E:driver():isValid()
D=E:driver()
holoAng(4,holoEntity(4):toWorld(ang((10*V)+T/10,0,0)))
E:applyAngForce(-ang(-E:angVel()[3],E:angVel()[2],E:angVel()[1])*E:mass())
if(H<=32)
{
    E:applyForce((-E:vel()+vec(0,0,abs(H-32)*20))*E:mass())
    E:applyAngForce((ang(E:angles():roll()*50,0,-E:angles():pitch()*50)-(ang(0,D:keyRight()-D:keyLeft(),0)*20))*E:mass())
}
if(H>32&V|T>650&V)
{
    Y=vec(D:eye():dot(E:right()),D:eye():dot(E:right()),0):y()
    P=vec(0,D:eye():dot(E:up()),D:eye():dot(E:up())):z()
    E:applyForce(E:toWorld(vec(-E:velL():x(),-E:velL():y(),-E:velL():z()/(1010-T))*E:mass()))  
    E:applyAngForce((ang(E:angles():roll(),0,-E:angles():pitch())+(ang(-P,-Y,Y)*100))*E:mass())
}
if(D:keyForward()&S&T<980){T+=3}
if(D:keyBack()|!V&T>0){T-=2}
if(T<0){T=0}
if(T>980){T=980}
E:applyForce(E:forward()*E:mass()*(T+Mul)*2*V)
Mul-=E:angles()[3]/10
if(Mul<-400){Mul=-400}
if(Mul>980){Mul=980}
if(Mul<0&E:angles()[3]>-35){Mul+=3}
if(Mul>0&E:angles()[3]<35){Mul-=0.1}
if(changed(D)&D)
{
    holoEntity(2):soundPlay(2,0,"vehicles/airboat/fan_motor_start1.wav")
    soundPitch(2,50)
    timer("engine",2000)
}
if(clk("engine")&V)
{    
    entity():soundPlay(1,0,"vehicles/airboat/fan_blade_idle_loop1.wav")
    S=1
}
soundPitch(1,clamp(E:vel():length()/10,50,250))
if(changed(V)&!V)
{
    S=0
    E:soundPlay(1,0,"vehicles/airboat/fan_motor_shut_off1.wav")
    Mul=0
    T=0
}
